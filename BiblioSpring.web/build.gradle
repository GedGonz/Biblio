plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.biblio'
version = ''
def jarName = project.name
java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

//se excluye el modulo de logging por defecto para usar log4j2
configurations.configureEach {
	exclude module: "spring-boot-starter-logging"
}
//

tasks.named('bootJar') {
	mainClass = 'com.biblio.web.WebApplication'
}


repositories {
	mavenCentral()
}

dependencies {

	//project
	implementation project(':BiblioSpring.infrastructure')
	implementation project(':BiblioSpring.domain')

	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//jwt
	implementation 'com.auth0:java-jwt:4.5.0'
	//cloudinary
	implementation 'com.cloudinary:cloudinary-http5:2.0.0'
	implementation 'io.github.cdimascio:dotenv-java:2.2.4'

	//log4j2
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'

	//jasper report
	implementation 'net.sf.jasperreports:jasperreports:7.0.1'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

}


bootJar {
	exclude '**/application.properties'
}

task distCopyPropertiFile(type: Copy){
	from 'src/main/resources/'
	include '**/*.properties'
	into 'build/tmp/dist'
}

task copyLibraries(type: Copy) {
	from(configurations.runtimeClasspath) {
		include '*.jar'
	}
	into 'build/libs'
}

task distCopyJarFile(type: Copy){
	dependsOn bootJar
	dependsOn copyLibraries
	from 'build/libs/' + jarName +'.jar'
	into 'build/tmp/dist'
}

task distCopyReports(type: Copy) {
	from 'src/main/resources/reports'
	into 'build/tmp/dist/reports'
}

tasks.named('test') {
	useJUnitPlatform()
}

task dist {
	dependsOn copyLibraries
	dependsOn distCopyJarFile
	dependsOn distCopyPropertiFile
	dependsOn distCopyReports
}
build.dependsOn dist